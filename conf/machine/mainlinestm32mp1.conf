#@TYPE: Machine
#@NAME: Mainline STM32MP1
#@SOC:  ST STM32MP1
#@DESCRIPTION: Machine configuration for Mainline STM32MP1

DEFAULTTUNE ?= "cortexa7thf-neon-vfpv4"
SOC_FAMILY ?= "stm32mp1"

ARM_INSTRUCTION_SET = "thumb"

require conf/machine/include/tune-cortexa7.inc
require conf/machine/include/soc-family.inc

MACHINE_FEATURES ?= "apm alsa ext2 keyboard screen serial touchscreen usbgadget usbhost"

SERIAL_CONSOLE = "115200 ttySTM0"

# U-Boot bootloader
UBOOT_ENTRYPOINT := "0xc4000000"
UBOOT_MACHINE = "stm32mp15_dhcom_basic_defconfig"
UBOOT_BINARY := "u-boot.img"
UBOOT_SUFFIX ?= "img"
SPL_BINARY := "u-boot-spl.stm32"
UBOOT_ENV = "boot"
UBOOT_ENV_SUFFIX = "scr"

PREFERRED_VERSION_u-boot ?= "2020.01%"
PREFERRED_VERSION_u-boot-mkimage ?= "2020.01%"

# Linux kernel
KERNEL_IMAGETYPE = "fitImage"
KERNEL_CLASSES += " kernel-fitimage "
KERNEL_DEVICETREE = "stm32mp157c-dhcom-pdk2.dtb"
KERNEL_MODULES_AUTOLOAD += " etnaviv "

PREFERRED_PROVIDER_virtual/kernel = "linux-stable"
PREFERRED_VERSION_linux-yocto = "5.4%"

# Ship kernel modules
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "kernel-modules kernel-devicetree linux-firmware-rs9116 u-boot"

# List of images to be built
IMAGE_FSTYPES ?= "tar.xz wic.xz"
INITRAMFS_FSTYPES ?= "cpio.xz"

WKS_FILE = "sdimage-mainlinestm32mp1.wks"
IMAGE_BOOT_FILES ?= "${KERNEL_IMAGETYPE}"
IMAGE_INSTALL_append = " kernel-devicetree kernel-image-fitimage u-boot"
do_image_wic[depends] += "mtools-native:do_populate_sysroot dosfstools-native:do_populate_sysroot u-boot:do_deploy"
